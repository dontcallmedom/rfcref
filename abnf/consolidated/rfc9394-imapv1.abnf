
capability = ("AUTH=" auth-type) / atom
sequence-set = (seq-number / seq-range) *("," sequence-set)
nz-number = digit-nz *DIGIT
auth-type = atom
atom = 1*ATOM-CHAR
seq-number = nz-number / "*"
seq-range = seq-number ":" seq-number
digit-nz = %x31-39
ATOM-CHAR = <any CHAR except atom-specials>


fetch-modifier = fetch-modifier-name [SP fetch-modif-params]
tagged-ext-simple = rfc4466-SEQUENCE-SET / number
fetch-modifier-name = tagged-ext-label
fetch-modif-params = tagged-ext-val
rfc4466-SEQUENCE-SET = (rfc3501-SEQ-NUMBER / rfc3501-SEQ-RANGE) *("," rfc4466-SEQUENCE-SET)
number = 1*DIGIT
tagged-ext-label = tagged-label-fchar *tagged-label-char
tagged-ext-val = tagged-ext-simple / "(" [tagged-ext-comp] ")"
rfc3501-SEQ-NUMBER = rfc3501-NZ-NUMBER / "*"
rfc3501-SEQ-RANGE = rfc3501-SEQ-NUMBER ":" rfc3501-SEQ-NUMBER
tagged-label-fchar = ALPHA / "-" / "_" / "."
tagged-label-char = tagged-label-fchar / DIGIT / ":"
tagged-ext-comp = astring / tagged-ext-comp *(SP tagged-ext-comp) / "(" tagged-ext-comp ")"
rfc3501-NZ-NUMBER = rfc3501-DIGIT-NZ *DIGIT
astring = 1*ASTRING-CHAR / string
rfc3501-DIGIT-NZ = %x31-39
ASTRING-CHAR = rfc3501-ATOM-CHAR / resp-specials
string = quoted / literal
rfc3501-ATOM-CHAR = <any CHAR except atom-specials>
resp-specials = "]"
quoted = DQUOTE *QUOTED-CHAR DQUOTE
literal = "{" number "}" CRLF *CHAR8
QUOTED-CHAR = <any TEXT-CHAR except quoted-specials> / "\" quoted-specials
CHAR8 = %x1-ff
quoted-specials = DQUOTE / "\"
; Extracted from IETF RFC 9394
; Copyright (c) IETF Trust and the persons identified as authors of the code. All rights reserved.
; Redistribution and use in source and binary forms, with or without modification, is permitted pursuant to, and subject to the license terms contained in, the Revised BSD License set forth in Section 4.c of the IETF Trust's Legal Provisions Relating to IETF Documents (https://trustee.ietf.org/license-info).

MINUS               = "-"

capability          =/ "PARTIAL"
                       ;; <capability> from [RFC3501].

modifier-partial    = "PARTIAL" SP partial-range

partial-range-first = nz-number ":" nz-number
    ;; Request to search from oldest (lowest UIDs) to
    ;; more recent messages.
    ;; A range 500:400 is the same as 400:500.
    ;; This is similar to <seq-range> from [RFC3501]
    ;; but cannot contain "*".

partial-range-last  = MINUS nz-number ":" MINUS nz-number
    ;; Request to search from newest (highest UIDs) to
    ;; oldest messages.
    ;; A range -500:-400 is the same as -400:-500.

partial-range       = partial-range-first / partial-range-last


    ;; All conform to <search-return-opt> from
    ;; [RFC4466] and [RFC9051].



ret-data-partial    = "PARTIAL"
                      SP "(" partial-range SP partial-results ")"
    ;; <partial-range> is the requested range.

partial-results     = sequence-set / "NIL"
    ;; <sequence-set> from [RFC3501].
    ;; NIL indicates that no results correspond to
    ;; the requested range.

tagged-ext-simple   =/ partial-range-last

fetch-modifier      =/ modifier-partial
                       ;; <fetch-modifier> from [RFC4466].
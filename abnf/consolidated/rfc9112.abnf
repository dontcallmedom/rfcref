
absolute-path = 1*("/" segment)
BWS = OWS
field-name = token
field-value = *field-content
obs-text = %x80-ff
OWS = *(SP / HTAB)
port = *DIGIT
query = *(pchar / "/" / "?")
quoted-string = DQUOTE *(qdtext / quoted-pair) DQUOTE
RWS = 1*(SP / HTAB)
token = 1*tchar
transfer-coding = token *(OWS ";" OWS transfer-parameter)
segment = *pchar
field-content = field-vchar [1*(SP / HTAB / field-vchar) field-vchar]
pchar = unreserved / pct-encoded / sub-delims / ":" / "@"
qdtext = HTAB / SP / "!" / %x23-5b / %x5d-7e / obs-text
quoted-pair = "\" (HTAB / SP / VCHAR / obs-text)
tchar = "!" / "#" / "$" / "%" / "&" / "'" / "*" / "+" / "-" / "." / "^" / "_" / "`" / "|" / "~" / DIGIT / ALPHA
transfer-parameter = token BWS "=" BWS (token / quoted-string)
field-vchar = VCHAR / obs-text
unreserved = ALPHA / DIGIT / "-" / "." / "_" / "~"
pct-encoded = "%" HEXDIG HEXDIG
sub-delims = "!" / "$" / "&" / "'" / "(" / ")" / "*" / "+" / "," / ";" / "="


absolute-URI = scheme ":" hier-part ["?" rfc3986-QUERY]
authority = [userinfo "@"] URI-HOST [":" rfc3986-PORT]
URI-HOST = IP-literal / IPv4address / reg-name
scheme = ALPHA *(ALPHA / DIGIT / "+" / "-" / ".")
hier-part = "//" authority path-abempty / path-absolute / path-rootless / path-empty
rfc3986-QUERY = *(rfc3986-PCHAR / "/" / "?")
userinfo = *(rfc3986-UNRESERVED / rfc3986-PCT-ENCODED / rfc3986-SUB-DELIMS / ":")
rfc3986-PORT = *DIGIT
IP-literal = "[" (IPv6address / IPvFuture) "]"
IPv4address = dec-octet "." dec-octet "." dec-octet "." dec-octet
reg-name = *(rfc3986-UNRESERVED / rfc3986-PCT-ENCODED / rfc3986-SUB-DELIMS)
path-abempty = *("/" rfc3986-SEGMENT)
path-absolute = "/" [segment-nz *("/" rfc3986-SEGMENT)]
path-rootless = segment-nz *("/" rfc3986-SEGMENT)
path-empty = 0<pchar>
rfc3986-PCHAR = rfc3986-UNRESERVED / rfc3986-PCT-ENCODED / rfc3986-SUB-DELIMS / ":" / "@"
rfc3986-UNRESERVED = ALPHA / DIGIT / "-" / "." / "_" / "~"
rfc3986-PCT-ENCODED = "%" HEXDIG HEXDIG
rfc3986-SUB-DELIMS = "!" / "$" / "&" / "'" / "(" / ")" / "*" / "+" / "," / ";" / "="
IPv6address = 6(h16 ":") ls32 / "::" 5(h16 ":") ls32 / [h16] "::" 4(h16 ":") ls32 / [[(h16 ":")] h16] "::" 3(h16 ":") ls32 / [*2(h16 ":") h16] "::" 2(h16 ":") ls32 / [*3(h16 ":") h16] "::" h16 ":" ls32 / [*4(h16 ":") h16] "::" ls32 / [*5(h16 ":") h16] "::" h16 / [*6(h16 ":") h16] "::"
IPvFuture = "v" 1*HEXDIG "." 1*(rfc3986-UNRESERVED / rfc3986-SUB-DELIMS / ":")
dec-octet = DIGIT / %x31-39 DIGIT / "1" 2DIGIT / "2" %x30-34 DIGIT / "25" %x30-35
rfc3986-SEGMENT = *rfc3986-PCHAR
segment-nz = 1*rfc3986-PCHAR
h16 = 1*4HEXDIG
ls32 = (h16 ":" h16) / IPv4address
; Extracted from IETF RFC 9112
; Copyright (c) IETF Trust and the persons identified as authors of the code. All rights reserved.
; Redistribution and use in source and binary forms, with or without modification, is permitted pursuant to, and subject to the license terms contained in, the Revised BSD License set forth in Section 4.c of the IETF Trust's Legal Provisions Relating to IETF Documents (https://trustee.ietf.org/license-info).


HTTP-message = start-line CRLF *( field-line CRLF ) CRLF [
 message-body ]
HTTP-name = %x48.54.54.50 ; HTTP
HTTP-version = HTTP-name "/" DIGIT "." DIGIT



Transfer-Encoding = [ transfer-coding *( OWS "," OWS transfer-coding
 ) ]

absolute-form = absolute-URI
asterisk-form = "*"
authority-form = uri-host ":" port

chunk = chunk-size [ chunk-ext ] CRLF chunk-data CRLF
chunk-data = 1*OCTET
chunk-ext = *( BWS ";" BWS chunk-ext-name [ BWS "=" BWS chunk-ext-val
 ] )
chunk-ext-name = token
chunk-ext-val = token / quoted-string
chunk-size = 1*HEXDIG
chunked-body = *chunk last-chunk trailer-section CRLF

field-line = field-name ":" OWS field-value OWS

last-chunk = 1*"0" [ chunk-ext ] CRLF

message-body = *OCTET
method = token

obs-fold = OWS CRLF RWS
origin-form = absolute-path [ "?" query ]



reason-phrase = 1*( HTAB / SP / VCHAR / obs-text )
request-line = method SP request-target SP HTTP-version
request-target = origin-form / absolute-form / authority-form /
 asterisk-form

start-line = request-line / status-line
status-code = 3DIGIT
status-line = HTTP-version SP status-code SP [ reason-phrase ]

trailer-section = *( field-line CRLF )
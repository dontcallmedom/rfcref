authority = [userinfo "@"] host [":" port]
dec-octet = DIGIT / %x31-39 DIGIT / "1" 2DIGIT / "2" %x30-34 DIGIT / "25" %x30-35
fragment = *(pchar / "/" / "?")
h16 = 1*4HEXDIG
hier-part = "//" authority path-abempty / path-absolute / path-rootless / path-empty
host = IP-literal / IPv4address / reg-name
IP-literal = "[" (IPv6address / IPvFuture) "]"
IPv4address = dec-octet "." dec-octet "." dec-octet "." dec-octet
IPv6address = 6(h16 ":") ls32 / "::" 5(h16 ":") ls32 / [h16] "::" 4(h16 ":") ls32 / [[(h16 ":")] h16] "::" 3(h16 ":") ls32 / [*2(h16 ":") h16] "::" 2(h16 ":") ls32 / [*3(h16 ":") h16] "::" h16 ":" ls32 / [*4(h16 ":") h16] "::" ls32 / [*5(h16 ":") h16] "::" h16 / [*6(h16 ":") h16] "::"
IPvFuture = "v" 1*HEXDIG "." 1*(unreserved / sub-delims / ":")
ls32 = (h16 ":" h16) / IPv4address
path-abempty = *("/" segment)
path-absolute = "/" [segment-nz *("/" segment)]
path-empty = 0<pchar>
path-noscheme = segment-nz-nc *("/" segment)
path-rootless = segment-nz *("/" segment)
pchar = unreserved / pct-encoded / sub-delims / ":" / "@"
pct-encoded = "%" HEXDIG HEXDIG
port = *DIGIT
query = *(pchar / "/" / "?")
reg-name = *(unreserved / pct-encoded / sub-delims)
relative-part = "//" authority path-abempty / path-absolute / path-noscheme / path-empty
relative-ref = relative-part ["?" query] ["#" fragment]
scheme = ALPHA *(ALPHA / DIGIT / "+" / "-" / ".")
segment = *pchar
segment-nz = 1*pchar
segment-nz-nc = 1*(unreserved / pct-encoded / sub-delims / "@")
sub-delims = "!" / "$" / "&" / "'" / "(" / ")" / "*" / "+" / "," / ";" / "="
unreserved = ALPHA / DIGIT / "-" / "." / "_" / "~"
URI = scheme ":" hier-part ["?" query] ["#" fragment]
URI-reference = URI / relative-ref
userinfo = *(unreserved / pct-encoded / sub-delims / ":")
access-token = 1*VSCHAR
client-id = *VSCHAR
client-secret = *VSCHAR
code = 1*VSCHAR
error = 1*NQSCHAR
error-description = 1*NQSCHAR
error-uri = URI-reference
expires-in = 1*DIGIT
grant-name = 1*name-char
grant-type = grant-name / URI-reference
name-char = "-" / "." / "_" / DIGIT / ALPHA
NQCHAR = %d33 / %x23-5b / %x5d-7e
NQSCHAR = %x20-21 / %x23-5b / %x5d-7e
param-name = 1*name-char
password = *UNICODECHARNOCRLF
redirect-uri = URI-reference
refresh-token = 1*VSCHAR
response-char = "_" / DIGIT / ALPHA
response-name = 1*response-char
response-type = response-name *(SP response-name)
scope = scope-token *(SP scope-token)
scope-token = 1*NQCHAR
state = 1*VSCHAR
token-type = type-name / URI-reference
type-name = 1*name-char
UNICODECHARNOCRLF = %d9 / %x20-7e / %x80-d7ff / %xe000-fffd / %x10000-10ffff
username = *UNICODECHARNOCRLF
VSCHAR = %x20-7e
Content-Format-Spec = Content-Format-Number / Content-Format-String
Content-Format-Number = "0" / (POS-DIGIT *DIGIT)
Content-Format-String = Content-Type *("@" Content-Coding)
Content-Type = Media-Type-Name *(*SP ";" *SP parameter)
parameter = token "=" (token / quoted-string)
token = 1*tchar
tchar = "!" / "#" / "$" / "%" / "&" / "'" / "*" / "+" / "-" / "." / "^" / "_" / "`" / "|" / "~" / DIGIT / ALPHA
quoted-string = %d34 *(qdtext / quoted-pair) %d34
qdtext = SP / %d33 / %x23-5b / %x5d-7e
quoted-pair = "\" (SP / VCHAR)
Content-Coding = token
Media-Type-Name = type-name "/" subtype-name
type-name = restricted-name
subtype-name = restricted-name
restricted-name = restricted-name-first *126restricted-name-chars
restricted-name-first = ALPHA / DIGIT
restricted-name-chars = ALPHA / DIGIT / "!" / "#" / "$" / "&" / "-" / "^" / "_" / "." / "+"
DIGIT = %x30-39
POS-DIGIT = %x31-39
ALPHA = %x41-5a / %x61-7a
SP = %d32
VCHAR = %x21-7e
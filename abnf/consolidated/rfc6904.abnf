URI = scheme ":" hier-part ["?" query] ["#" fragment]
scheme = ALPHA *(ALPHA / DIGIT / "+" / "-" / ".")
hier-part = "//" authority path-abempty / path-absolute / path-rootless / path-empty
query = *(pchar / "/" / "?")
fragment = *(pchar / "/" / "?")
authority = [userinfo "@"] host [":" port]
path-abempty = *("/" segment)
path-absolute = "/" [segment-nz *("/" segment)]
path-rootless = segment-nz *("/" segment)
path-empty = 0<pchar>
pchar = unreserved / pct-encoded / sub-delims / ":" / "@"
userinfo = *(unreserved / pct-encoded / sub-delims / ":")
host = IP-literal / IPv4address / reg-name
port = *DIGIT
segment = *pchar
segment-nz = 1*pchar
unreserved = ALPHA / DIGIT / "-" / "." / "_" / "~"
pct-encoded = "%" HEXDIG HEXDIG
sub-delims = "!" / "$" / "&" / "'" / "(" / ")" / "*" / "+" / "," / ";" / "="
IP-literal = "[" (IPv6address / IPvFuture) "]"
IPv4address = dec-octet "." dec-octet "." dec-octet "." dec-octet
reg-name = *(unreserved / pct-encoded / sub-delims)
IPv6address = 6(h16 ":") ls32 / "::" 5(h16 ":") ls32 / [h16] "::" 4(h16 ":") ls32 / [[(h16 ":")] h16] "::" 3(h16 ":") ls32 / [*2(h16 ":") h16] "::" 2(h16 ":") ls32 / [*3(h16 ":") h16] "::" h16 ":" ls32 / [*4(h16 ":") h16] "::" ls32 / [*5(h16 ":") h16] "::" h16 / [*6(h16 ":") h16] "::"
IPvFuture = "v" 1*HEXDIG "." 1*(unreserved / sub-delims / ":")
dec-octet = DIGIT / %x31-39 DIGIT / "1" 2DIGIT / "2" %x30-34 DIGIT / "25" %x30-35
h16 = 1*4HEXDIG
ls32 = (h16 ":" h16) / IPv4address






byte-string = 1*(%x1-9 / %xb-c / %xe-ff)
mapentry = "extmap:" 1*5DIGIT ["/" direction]
extmap = mapentry SP extensionname [SP extensionattributes]
extensionname = rfc5285-URI
extensionattributes = rfc5285-BYTE-STRING
direction = "sendonly" / "recvonly" / "sendrecv" / "inactive"
rfc5285-URI = <Defined in RFC 3986>
rfc5285-BYTE-STRING = <Defined in RFC 4566>
; Extracted from IETF RFC 6904
; Copyright (c) IETF Trust and the persons identified as authors of the code. All rights reserved.
; Redistribution and use in source and binary forms, with or without modification, is permitted pursuant to, and subject to the license terms contained in, the Revised BSD License set forth in Section 4.c of the IETF Trust's Legal Provisions Relating to IETF Documents (https://trustee.ietf.org/license-info).

enc-extensionname = %x75.72.6e.3a.69.65.74.66.3a.70.61.72.61.6d.73.3a
       %x72.74.70.2d.68.64.72.65.78.74.3a.65.6e.63.72.79.70.74
       ; "urn:ietf:params:rtp-hdrext:encrypt" in lower case

extmap =/ mapentry SP enc-extensionname SP extensionname
       [SP extensionattributes]
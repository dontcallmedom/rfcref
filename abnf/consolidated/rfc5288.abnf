argument = string-list / number / tag
arguments = *argument [test / test-list]
block = "{" commands "}"
bracket-comment = "/*" *not-star 1*STAR *(not-star-slash *not-star 1*STAR) "/"
command = identifier arguments (";" / block)
commands = *command
comment = bracket-comment / hash-comment
hash-comment = "#" *octet-not-crlf CRLF
identifier = (ALPHA / "_") *(ALPHA / DIGIT / "_")
multi-line = "text:" *(SP / HTAB) (hash-comment / CRLF) *(multiline-literal / multiline-dotstart) "." CRLF
multiline-dotstart = "." 1*octet-not-crlf CRLF
multiline-literal = [octet-not-period *octet-not-crlf] CRLF
not-star = CRLF / %x1-9 / %xb-c / %xe-29 / %x2b-ff
not-star-slash = CRLF / %x1-9 / %xb-c / %xe-29 / %x2b-2e / %x30-ff
number = 1*DIGIT [QUANTIFIER]
octet-not-crlf = %x1-9 / %xb-c / %xe-ff
octet-not-period = %x1-9 / %xb-c / %xe-2d / %x2f-ff
octet-not-qspecial = %x1-9 / %xb-c / %xe-21 / %x23-5b / %x5d-ff
QUANTIFIER = "K" / "M" / "G"
quoted-other = "\" octet-not-qspecial
quoted-safe = CRLF / octet-not-qspecial
quoted-special = "\" (DQUOTE / "\")
quoted-string = DQUOTE quoted-text DQUOTE
quoted-text = *(quoted-safe / quoted-special / quoted-other)
STAR = "*"
start = commands
string = quoted-string / multi-line
string-list = "[" string *("," string) "]" / string
tag = ":" identifier
test = identifier arguments
test-list = "(" test *("," test) ")"
white-space = 1*(SP / CRLF / HTAB) / comment
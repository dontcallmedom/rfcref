; Extracted from IETF RFC 9073
; Copyright (c) IETF Trust and the persons identified as authors of the code. All rights reserved.
; Redistribution and use in source and binary forms, with or without modification, is permitted pursuant to, and subject to the license terms contained in, the Revised BSD License set forth in Section 4.c of the IETF Trust's Legal Provisions Relating to IETF Documents (https://trustee.ietf.org/license-info).

; Addition of PARTICIPANT, VLOCATION, and VRESOURCE
; as valid components
eventc     = "BEGIN" ":" "VEVENT" CRLF
             eventprop *alarmc *participantc *locationc *resourcec
             "END" ":" "VEVENT" CRLF

; Addition of properties STYLED-DESCRIPTION and STRUCTURED-DATA
eventprop  =/ *styleddescription
              *sdataprop

; Addition of PARTICIPANT, VLOCATION, and VRESOURCE
; as valid components
todoc      = "BEGIN" ":" "VTODO" CRLF
             todoprop *alarmc *participantc *locationc *resourcec
             "END" ":" "VTODO" CRLF

; Addition of properties STYLED-DESCRIPTION and STRUCTURED-DATA
todoprop =/ *styleddescription
            *sdataprop

; Addition of PARTICIPANT, VLOCATION, and VRESOURCE
; as valid components
journalc   = "BEGIN" ":" "VJOURNAL" CRLF
             jourprop *participantc *locationc *resourcec
             "END" ":" "VJOURNAL" CRLF

; Addition of properties STYLED-DESCRIPTION and STRUCTURED-DATA
jourprop =/ *styleddescription
            *sdataprop

; Addition of PARTICIPANT, VLOCATION, and VRESOURCE
; as valid components
freebusyc  = "BEGIN" ":" "VFREEBUSY" CRLF
             fbprop *participantc *locationc *resourcec
             "END" ":" "VFREEBUSY" CRLF

; Addition of property STYLED-DESCRIPTION
fbprop     =/ *styleddescription


orderparam    = "ORDER" "=" integer
                ;           Must be greater than or equal to 1


schemaparam    = "SCHEMA" "=" DQUOTE uri DQUOTE


derivedparam    = "DERIVED" "=" ("TRUE" / "FALSE")
; Default is FALSE


loctype      = "LOCATION-TYPE" loctypeparam ":"
               text *("," text)
               CRLF

loctypeparam   = *(";" other-param)


participanttype   = "PARTICIPANT-TYPE" partvalueparam ":"
                     partvalue CRLF

partvalue    = ("ACTIVE"
               / "INACTIVE"
               / "SPONSOR"
               / "CONTACT"
               / "BOOKING-CONTACT"
               / "EMERGENCY-CONTACT"
               / "PUBLICITY-CONTACT"
               / "PLANNER-CONTACT"
               / "PERFORMER"
               / "SPEAKER"
               / iana-token)     ; Other IANA-registered
                                 ; values

partvalueparam   = *(";" other-param)


restypeprop   = "RESOURCE-TYPE" restypeparam ":"
                restypevalue CRLF

restypevalue  = ("ROOM"
               / "PROJECTOR"
               / "REMOTE-CONFERENCE-AUDIO"
               / "REMOTE-CONFERENCE-VIDEO"
               / iana-token)     ; Other IANA-registered
               ; values

restypeparam   = *(";" other-param)


calendaraddress   = "CALENDAR-ADDRESS" caladdressparam ":"
               cal-address CRLF

caladdressparam   = *(";" other-param)


styleddescription = "STYLED-DESCRIPTION" styleddescparam ":"
                   styleddescval CRLF

styleddescparam   = *(
                   ; The following is REQUIRED
                   ; but MUST NOT occur more than once.
                   ;
                   (";" "VALUE" "=" ("URI" / "TEXT")) /
                   ;
                   ; The following are OPTIONAL
                   ; but MUST NOT occur more than once.
                   ;
                   (";" altrepparam) / (";" languageparam) /
                   (";" fmttypeparam) / (";" derivedparam) /
                   ;
                   ; The following is OPTIONAL
                   ; and MAY occur more than once.
                   ;
                   (";" other-param)
                   )

styleddescval   = ( uri / text )
;Value MUST match value type


participantc = "BEGIN" ":" "PARTICIPANT" CRLF
               partprop *locationc *resourcec
               "END" ":" "PARTICIPANT" CRLF

partprop     = *(
               ;
               ; The following are REQUIRED
               ; but MUST NOT occur more than once.
               ;
               participanttype / uid /
               ;
               ; The following are OPTIONAL
               ; but MUST NOT occur more than once.
               ;
               calendaraddress / created / description / dtstamp /
               geo / last-mod / priority / seq /
               status / summary / url /
               ;
               ; The following are OPTIONAL
               ; and MAY occur more than once.
               ;
               attach / categories / comment
               contact / location / rstatus / related /
               resources /
               styleddescription / sdataprop / iana-prop
               ;
               )


locationc    = "BEGIN" ":" "VLOCATION" CRLF
               locprop
               "END" ":" "VLOCATION" CRLF

locprop      = *(
               ;
               ; The following are REQUIRED
               ; but MUST NOT occur more than once.
               ;
               uid /
               ;
               ; The following are OPTIONAL
               ; but MUST NOT occur more than once.
               ;
               description / geo / loctype / name / url /
               ;
               ; The following are OPTIONAL
               ; and MAY occur more than once.
               ;
               sdataprop / iana-prop
            )


resourcec    = "BEGIN" ":" "VRESOURCE" CRLF
               resprop
               "END" ":" "VRESOURCE" CRLF

resprop      = *(
               ;
               ; The following are REQUIRED
               ; but MUST NOT occur more than once.
               ;
               uid /
               ;
               ; The following are OPTIONAL
               ; but MUST NOT occur more than once.
               ;
               description / geo / name / restypeprop /
               ;
               ; The following are OPTIONAL
               ; and MAY occur more than once.
               ;
               sdataprop / iana-prop
             )

sdataprop    = "STRUCTURED-DATA" sdataparam
                 (
                   ";" "VALUE" "=" "TEXT"
                   ":" text
                 ) /
                 (
                   ";" "ENCODING" "=" "BASE64"
                   ";" "VALUE" "=" "BINARY"
                   ";" binary
                 ) /
                 (
                   ";" "VALUE" "=" "URI"
                   ":" uri
                 )
                 CRLF

sdataparam  = *(
                 ;
                 ; The following is OPTIONAL for a URI value,
                 ; REQUIRED for a TEXT or BINARY value,
                 ; and MUST NOT occur more than once.
                 ;
                 (";" fmttypeparam) /
                 (";" schemaparam) /
                 ;
                 ; The following is OPTIONAL
                 ; and MAY occur more than once.
                 ;
                 (";" other-param)
                 ;
              )